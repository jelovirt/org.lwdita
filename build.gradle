apply plugin: "java"
apply plugin: "maven"

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.elovirta.dita'
version = "0.5.1"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: "org.dita-ot", name: "dost", version: "2.0"
    compile group: "org.pegdown", name: "pegdown", version: "1.4.2"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task dist(type: Zip) {
    from configurations.runtime.allArtifacts.files
    from configurations.runtime
    from "src/main"
    include { details -> details.file.name.contains('markdown') }
    include { details -> details.file.name.contains('pegdown') }
    include { details -> details.file.name.contains('asm-') }
    include { details -> details.file.name.contains('parboiled-') }
    from("src/main") {
        include "plugin.xml"
        expand(id: "${project.group}.${project.name}", version: project.version)
    }
    from("src/main") {
        include "build_template.xml"
        include "conductor.xml"
        include "xsl/*.xsl"
        // Template generated files
        exclude "build.xml"
        exclude "xsl/dita2markdown.xsl"
        exclude "xsl/map2markdown-cover.xsl"
    }
    from(".") {
        include "LICENSE"
        exclude "*.iml"
    }
    archiveName "${project.group}.${project.name}_${project.version}.zip"
}